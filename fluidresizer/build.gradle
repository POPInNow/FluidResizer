/*
 * Copyright (C) 2018 POP Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: "com.getkeepsafe.dexcount"
apply plugin: 'org.jetbrains.dokka-android'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

android {
  compileSdkVersion rootProject.ext.maxSdk
  defaultConfig {
    minSdkVersion rootProject.ext.minSdk

    // English only, no translations at this time.
    resConfigs "en"

    // Ship with Proguard files
    consumerProguardFiles 'proguard-rules.pro'
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles 'proguard-rules.pro'
    }
  }

  // TODO replace with https://issuetracker.google.com/issues/72050365 once released.
  libraryVariants.all {
    it.generateBuildConfig.enabled = false
  }
}

/**
 * We need to bundle the test classes so that other tests can extend from them
 */
task resizerDebugTestJar(type: Jar) {
  classifier = 'test'
  from "$buildDir/tmp/kotlin-classes/debugUnitTest"
}

resizerDebugTestJar.dependsOn tasks.withType(AbstractCompile).matching {
  def name = it.name.toLowerCase()
  name.contains("debug") && name.contains("unit") && name.contains("test")
}

task resizerReleaseTestJar(type: Jar) {
  classifier = 'test'
  from "$buildDir/tmp/kotlin-classes/releaseUnitTest"
}

resizerReleaseTestJar.dependsOn tasks.withType(AbstractCompile).matching {
  def name = it.name.toLowerCase()
  name.contains("release") && name.contains("unit") && name.contains("test")
}

artifacts {
  archives resizerDebugTestJar
  archives resizerReleaseTestJar
}

dependencies {
  implementation "androidx.interpolator:interpolator:$interpolator"
  implementation "androidx.lifecycle:lifecycle-runtime:$lifecycle"
  implementation "androidx.fragment:fragment:$fragment"
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

  testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

apply from: "$rootDir/publishing.gradle"

