/*
 * Copyright (C) 2019 POP Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * Publish the source code with the library
 */
task androidSourcesJar(type: Jar) {
  classifier = 'sources'
  from android.sourceSets.main.java.source
}

/**
 * Publish javadoc with library
 */
task androidJavadocsJar(type: Jar, dependsOn: dokka) {
  classifier = 'javadoc'
  from "$buildDir/dokka"
}

artifacts {
  archives androidSourcesJar
  archives androidJavadocsJar
}

publishing {
  publications {
    FluidResizer(MavenPublication) {
      artifact bundleReleaseAar
      artifact androidSourcesJar
      artifact androidJavadocsJar
      groupId POM_GROUP
      artifactId project.name
      version POM_VERSION

      pom.withXml {
        def dependenciesNode = asNode().appendNode('dependencies')

        configurations.implementation.allDependencies.each {
          // Ensure dependencies such as fileTree are not included in the pom.
          if (it.name != 'unspecified') {
            def dependencyNode = dependenciesNode.appendNode('dependency')
            dependencyNode.appendNode('groupId', it.group)
            dependencyNode.appendNode('artifactId', it.name)
            dependencyNode.appendNode('version', it.version)
          }
        }
      }
    }
  }
}

final String GITHUB_ROOT_URL = "https://github.com/POPinNow/FluidResizer"
bintray {
  user = 'popindeveloper'
  key = BINTRAY_API_KEY
  publications = ['FluidResizer']
  override = true
  pkg {
    repo = 'FluidResizer'
    name = project.name
    desc = "Implementation of resize animation from https://github.com/saket/FluidKeyboardResize"
    licenses = ['Apache-2.0']
    vcsUrl = "${GITHUB_ROOT_URL}"
    websiteUrl = "${GITHUB_ROOT_URL}"
    version {
      name = POM_VERSION
      issueTrackerUrl = "${GITHUB_ROOT_URL}/issues"
      desc = "Implementation of resize animation from https://github.com/saket/FluidKeyboardResize"
      released = new Date()
      vcsTag = POM_VERSION
    }
  }
}

